plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = 'net.arnx'
version = '0.1.3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	jcenter()
}

dependencies {
	testCompile 'junit:junit:4.12'
}

task dist() {
	doLast {
		copy {
			from file('src/main/resources/net/arnx/nashorn/lib/promise.js')
			rename(/promise.js$/, 'index.js')
			into '.'
		}
	}
}
jar.dependsOn(dist);

compileJava {
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

javadoc {
	options {
		links(JavaVersion.current().isJava11Compatible() ? 
			'https://docs.oracle.com/en/java/javase/11/docs/api' : 
			'https://docs.oracle.com/javase/8/docs/api/')
		locale = 'en_US'
		encoding = 'UTF-8'
		docEncoding = 'UTF-8'
		charSet = 'UTF-8'
		author = true
	}
	options.addBooleanOption('html5', 
		JavaVersion.current().isJava9Compatible())
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'nashorn-promise'
                description = 'ES6 Promise polyfil for Nashorn'
                url = 'https://github.com/hidekatsu-izuno/nashorn-promise'

				licenses {
					license {
						name = 'MIT License'
						url = 'http://www.opensource.org/licenses/mit-license.php'
					}
				}
				
				developers {
					developer {
						id = 'hidekatsu.izuno'
						name = 'Hidekatsu Izuno'
						email = 'hidekatsu.izuno@gmail.com'
					}
				}

				scm {
					url = 'https://github.com/hidekatsu-izuno/nashorn-promise'
					connection = 'scm:git:git://github.com/hidekatsu-izuno/nashorn-promise.git'
					developerConnection = 'scm:git:ssh:git@github.com:hidekatsu-izuno/nashorn-promise.git'
				}
			}
		}
	}
	
	repositories {
		maven {
			url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username = sonatypeUsername
				password = sonatypePassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}